uniform mat4 landmatrix;  // matrix for land points
uniform mat4 modelmatrix; // Matrix for model points
uniform mat4 control;     // Control matrix containing elevator, rudder
                          // and airleron values.

attribute vec4 g_vVertex;
attribute vec4 g_vColor;
attribute vec2 g_vData;   // data[0]=ctrl   data[1]=value
                          // ctrl = 0.0  for fixed points on the aircraft
                          // ctrl = 1.0  for rudder
                          // ctrl = 2.0  for elevator
                          // ctrl = 3.0  for left aileron
                          // ctrl = 4.0  for right aileron
                          // ctrl = 5.0  for point on the land
varying   vec4 g_vVSColor;

void main()
{ float ctrl = g_vData[0];
  // 1.0 rudder 2.0 elevator 3.0 left aileron 4.0 right aileron

  g_vVSColor.r = g_vColor.r;
  g_vVSColor.g = g_vColor.g;
  g_vVSColor.b = g_vColor.b;
  //g_vVSColor.b = 1.0;

  // For fun, use the xyz coordinates to adjust the colour a little
  //g_vVSColor = g_vColor*0.9 + g_vVertex * 0.010;

  // Deal with the control surfaces

  if(ctrl > 0.0) {
    float dist = g_vData[1];

    vec4 Pos = g_vVertex;

    Pos.w = 1.0;

    if(ctrl==1.0) { // Rudder
      float cr = control[0][0];
      float sr = control[0][1];
      Pos.x += dist * (1.0-cr);
      Pos.y += dist * sr;
    }
    if(ctrl==2.0) { // Elevator
      float ce = control[0][2];
      float se = control[0][3];
      Pos.x += dist * (1.0 - ce);
      Pos.z += dist * se;
    }
    if(ctrl==3.0) { // Left aileron
      float ca = control[1][0];
      float sa = control[1][1];
      Pos.x += dist * (1.0 - ca);
      Pos.z += dist * sa;
    }
    if(ctrl==4.0) { // Right aileron
      float ca = control[1][0];
      float sa = control[1][1];
      Pos.x += dist * (1.0 - ca);
      Pos.z -= dist * sa;
    }

    if(ctrl==5.0) { // Points on the land
      gl_Position = (landmatrix * g_vVertex);
      return;
    }

    // Rotate and translate the control surface
    gl_Position = (modelmatrix * Pos);
    return;

  }

  // Rotate and translate the model
  gl_Position = (modelmatrix * g_vVertex);
}
